
//////////////////////////////////////////////////////////////////////
// Глобальные переменные

&НаКлиенте
Перем ОбработчикИзмененияСостоянияАгента;

//////////////////////////////////////////////////////////////////////
// Обработчики команд

&НаКлиенте
Процедура Сохранить()
	
	РаботаСХранилищемОбщихНастроек.СохранитьРабочийКаталог(ПутьКРабочемуКаталогу);
	РаботаСХранилищемОбщихНастроек.СохранитьТекстКраткогоЗаголовкаПриложения(КраткийЗаголовок);
	
	КраткийЗаголовокИзменился = Ложь;
	Если ПолучитьКраткийЗаголовокПриложения() <> КраткийЗаголовок Тогда
		КраткийЗаголовокИзменился = Истина;
		УстановитьКраткийЗаголовокПриложения(КраткийЗаголовок);
	КонецЕсли;
	
	Если АгентДоступен Тогда
		ТекущееСостояние = АгентКлиентскогоПриложения.ПолучитьТекущееСостояние();
		Если (АгентПодключен = 1 И (ТекущееСостояние = СостояниеАгентаКлиентскогоПриложения.Отключен Или КраткийЗаголовокИзменился)) Тогда
			Если КраткийЗаголовок = "" Тогда
				ПоказатьПредупреждение(, НСтр("ru ='Для включения оповещений при закрытом приложении необходимо задать краткий заголовок приложения'", "ru"));
			Иначе
				АгентКлиентскогоПриложения.НачатьПодключение();
				АгентКлиентскогоПриложения.УстановитьНаименованиеПриложения(КраткийЗаголовок);
				Закрыть(Истина);
			КонецЕсли;
		ИначеЕсли (АгентПодключен = 0 И ТекущееСостояние = СостояниеАгентаКлиентскогоПриложения.Подключен) Тогда
			АгентКлиентскогоПриложения.НачатьОтключение();
			Закрыть(Истина);
		Иначе
			Закрыть(Истина);
		КонецЕсли;
	Иначе
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПутьКРабочемуКаталогу = РаботаСХранилищемОбщихНастроек.ПолучитьРабочийКаталог();
	КраткийЗаголовок = РаботаСХранилищемОбщихНастроек.ПолучитьТекстКраткогоЗаголовкаПриложения();
	АгентДоступен = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если Не МобильныйКлиент Тогда
		СистемИнфо = Новый СистемнаяИнформация();
		АгентДоступен = СистемаВзаимодействия.ИспользованиеДоступно() И
			(Не СистемИнфо.ТипПлатформы = ТипПлатформы.MacOS_x86) И
			(Не СистемИнфо.ТипПлатформы = ТипПлатформы.MacOS_x86_64);
	#КонецЕсли
	
	Если АгентДоступен Тогда
		Элементы.ОповещенияПриЗакрытомКлиентскомПриложенииГруппа.Видимость = Истина;
		ОбработчикИзмененияСостоянияАгента = Новый ОписаниеОповещения("ПриИзмененииСостоянияАгента", ЭтотОбъект);
		АгентКлиентскогоПриложения.ПодключитьОбработчикИзмененияСостояния(ОбработчикИзмененияСостоянияАгента);
		ТекущееСостояние = АгентКлиентскогоПриложения.ПолучитьТекущееСостояние();
		АгентЗапущен = Не (ТекущееСостояние = СостояниеАгентаКлиентскогоПриложения.НеЗапущен);
		АгентПодключен = ?(ТекущееСостояние = СостояниеАгентаКлиентскогоПриложения.Подключен, 1, 0);
		ОбновитьЭлементыАгента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если АгентДоступен Тогда
		АгентКлиентскогоПриложения.ОтключитьОбработчикИзмененияСостояния(ОбработчикИзмененияСостоянияАгента);
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////
// Обработчики событий элементов управления

&НаКлиенте
Процедура ПутьКРабочемуКаталогуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОпПослеПодключенияРасширения = Новый ОписаниеОповещения("ПослеПодключенияРасширения", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСФайлами(ОпПослеПодключенияРасширения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияРасширения(Подключено, ДопПараметры) Экспорт
	
	Если НЕ Подключено Тогда
		ПоказатьПредупреждение(, НСтр("ru ='Данная возможность недоступна, так как не подключено расширение работы с файлами.'", "ru"));
	КонецЕсли;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогОткрытияФайла.Заголовок = НСтр("ru ='Выбор каталога временного хранения файлов'", "ru");
	ОпПослеВыбораКаталога = Новый ОписаниеОповещения("ПослеВыбораКаталога", ЭтотОбъект);
	ДиалогОткрытияФайла.Показать(ОпПослеВыбораКаталога);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКаталога(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено Тогда
		ПутьКРабочемуКаталогу = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитеАгентОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АгентКлиентскогоПриложения.НачатьУстановку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСостоянияАгента(Состояние, ДопПараметры) Экспорт
	
	ТекущееСостояние = АгентКлиентскогоПриложения.ПолучитьТекущееСостояние();
	АгентЗапущен = Не (ТекущееСостояние = СостояниеАгентаКлиентскогоПриложения.НеЗапущен);
	Если АгентЗапущен Тогда
		АгентПодключен = 1;
	КонецЕсли;
	
	ОбновитьЭлементыАгента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЭлементыАгента()
	
	ВерсияАктуальна = АгентКлиентскогоПриложения.ВерсияАктуальна();
	Элементы.АгентПодключен.Доступность = АгентЗапущен;
	Элементы.ОбновитеАгент.Видимость = АгентЗапущен И (Не ВерсияАктуальна);
	Элементы.УстановитеАгент.Видимость = Не АгентЗапущен;
	
КонецПроцедуры


